extends Node3D

@export var squad_size := 9
@export var formation_spacing := 0.6
@export var owner_id := 1
@export var grunt_scene: PackedScene

@onready var nav_agent: NavigationAgent3D = $NavigationAgent3D
@onready var container: Node = $grunt_container

var soldiers: Array = []

func formation_offsets() -> Array:
	return [
		Vector3(0, 0, 0), Vector3(1, 0, 0), Vector3(-1, 0, 0),
		Vector3(0, 0, -1), Vector3(0, 0, 1), Vector3(1, 0, -1),
		Vector3(-1, 0, -1), Vector3(1, 0, 1), Vector3(-1, 0, 1)
	]

func _ready():
	if grunt_scene == null:
		grunt_scene = preload("res://assets/infantry/player1/grunt.tscn")
	spawn_squad()

func spawn_squad():
	var offsets = formation_offsets()
	for i in squad_size:
		var grunt = grunt_scene.instantiate()
		container.add_child(grunt)
		
		var offset := Vector3.ZERO
		if i < offsets.size():
			offset = offsets[i] * formation_spacing
		
		var spawn_pos = global_position + offset
		grunt.global_position = NavigationServer3D.map_get_closest_point(nav_agent.get_navigation_map(), spawn_pos)
		
		grunt.set_squad(self)
		grunt.owner_id = owner_id
		soldiers.append(grunt)

func move_to(target: Vector3):
	var center = NavigationServer3D.map_get_closest_point(nav_agent.get_navigation_map(), target)
	nav_agent.target_position = center

	var direction = (center - global_position).normalized()
	var angle = atan2(direction.x, direction.z)

	var offsets = formation_offsets()
	for i in soldiers.size():
		if i >= offsets.size():
			continue
		
		var offset = offsets[i] * formation_spacing
		offset = offset.rotated(Vector3.UP, angle)
		var unit_target = center + offset

		soldiers[i].move_to(unit_target)

func set_selected(state: bool, color: Color = Color.GREEN):
	for soldier in soldiers:
		soldier.set_selected(state, color)

func on_squad_selected():
	var sm_path = "/root/rootGrid/Player%d/select_manager" % owner_id
	if has_node(sm_path):
		get_node(sm_path).select_squad(self)
